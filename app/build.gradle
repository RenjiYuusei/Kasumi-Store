plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

import java.io.InputStreamReader

android {
    namespace 'com.kasumi.tool'
    compileSdk 34

    defaultConfig {
        applicationId 'com.kasumi.tool'
        minSdk 24
        targetSdk 34
        versionCode 8
        versionName "1.5.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildFeatures {
        compose true
    }

    sourceSets {
        main {
            assets.srcDirs += ['../source']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Signing config is set below if keystore.properties exists
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        resources {
            excludes += ['/META-INF/{AL2.0,LGPL2.1}']
        }
    }
}

// Signing config: load from keystore.properties if present (CI populates it)
def keystorePropsFile = rootProject.file('keystore.properties')
if (keystorePropsFile.exists()) {
    def keystoreProps = new Properties()
    keystorePropsFile.withInputStream { stream ->
        keystoreProps.load(new InputStreamReader(stream, java.nio.charset.StandardCharsets.UTF_8))
    }

    def storeFilePath = (keystoreProps.getProperty('storeFile') ?: keystoreProps.getProperty('\ufeffstoreFile'))?.trim()
    def storePasswordValue = keystoreProps.getProperty('storePassword')
    def keyAliasValue = keystoreProps.getProperty('keyAlias')
    def keyPasswordValue = keystoreProps.getProperty('keyPassword')

    def storeFileResolved = storeFilePath ? keystorePropsFile.parentFile.toPath().resolve(storeFilePath).toFile() : null

    def missingKeys = []
    if (!storeFilePath) missingKeys << 'storeFile'
    if (!storePasswordValue) missingKeys << 'storePassword'
    if (!keyAliasValue) missingKeys << 'keyAlias'
    if (!keyPasswordValue) missingKeys << 'keyPassword'
    if (storeFileResolved && !storeFileResolved.exists()) missingKeys << "keystore file (${storeFileResolved})"

    if (missingKeys.isEmpty()) {
        android {
            signingConfigs {
                release {
                    storeFile storeFileResolved
                    storePassword storePasswordValue
                    keyAlias keyAliasValue
                    keyPassword keyPasswordValue
                }
            }
            buildTypes {
                release {
                    signingConfig signingConfigs.release
                }
            }
        }
    } else {
        logger.lifecycle("Skipping release signing config; ${missingKeys.join(', ')} missing or invalid in keystore.properties.")
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.activity:activity-ktx:1.9.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.6'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
    
    // Glide for image loading
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    // Compose
    implementation platform('androidx.compose:compose-bom:2024.04.01')
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'io.coil-kt:coil-compose:2.6.0'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1'
}

tasks.withType(Test) {
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
}
